apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = 'fr.javatic.gradle'
version = '1.0'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile "org.eclipse.jgit:org.eclipse.jgit:3.3.2.201404171909-r"
}

tasks['javadoc'].failOnError false

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
}

signing {
    sign configurations.archives
    sign sourceJar
    sign javadocJar
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }

            pom.withXml {
                asNode().appendNode('name', "$group:$name")
                asNode().appendNode('url', 'https://github.com/ylemoigne/gradle-release')
                asNode().appendNode('description', 'Opiniated & Simple Gradle release plugin supporting Git')

                def licenseNode = asNode().appendNode('licenses').appendNode('license')
                licenseNode.appendNode('name', "The Apache Software License, Version 2.0")
                licenseNode.appendNode('url', "http://www.apache.org/licenses/LICENSE-2.0.txt")

                def developerNode = asNode().appendNode('developers').appendNode('developer')
                developerNode.appendNode('name', "Yann Le Moigne")
                developerNode.appendNode('email', "ylemoigne@javatic.fr")
                developerNode.appendNode('organization', "JavaTIC")
                developerNode.appendNode('organizationUrl', "http://www.javatic.fr")

                def scmNode = asNode().appendNode('scm')
                scmNode.appendNode('connection', "scm:git:git@github.com:ylemoigne/gradle-release.git")
                scmNode.appendNode('developerConnection', "scm:git:git@github.com:ylemoigne/gradle-release.git")
                scmNode.appendNode('url', "git@github.com:ylemoigne/gradle-release.git")
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
        }
    }
}